//
// Lavendeux Parser Grammar Definition
// Copyright Richard Carson, 2023
//

SCRIPT = {
    LINE* ~ &EOI
}

LINE = _{
    | WHITESPACE* ~ STATEMENT ~ (EOL | &EOI)
    | WHITESPACE* ~ (EOL | INLINE_COMMENT)
}

//
// Tokenizer configuration
//

INLINE_COMMENT = @{"//" ~ (!EOL ~ ANY)* ~ (&EOL | &EOI)}
BLOCK_COMMENT = @{"/*" ~ (!"*/" ~ ANY)* ~ "*/"}
COMMENT = _{INLINE_COMMENT | BLOCK_COMMENT}

WHITESPACE = _{" " | "\t" | "\\" ~ EOL | COMMENT}
EOL = _{NEWLINE | ";"}

//
// Top of expression tree
//

STATEMENT = _{
    | FUNCTION_ASSIGNMENT_STATEMENT
    | EXPR
}

function_name = @{symbol_at? ~ identifier}
FUNCTION_ASSIGNMENT_STATEMENT = {
    | EOL* ~ function_name ~ EOL* ~ 
        "(" ~ EOL* ~ 
            (function_argument ~ EOL* ~ (symbol_comma ~ EOL* ~ function_argument ~ EOL*)* ~ symbol_comma? ~ EOL*)? ~ 
        ")" ~ (EOL* ~ function_typespec)? ~ EOL* ~ 
        symbol_eq ~ EOL* ~ BLOCK
}
	function_typespec = {symbol_colon ~ EOL* ~ identifier}
	function_argument = {identifier ~ EOL* ~ (symbol_colon ~ EOL* ~ identifier)?}

BLOCK = {
    | symbol_opencurly ~ block_line* ~ (EXPR)? ~ EOL? ~ symbol_closecurly
    | EXPR
}
    block_line = _{
        | WHITESPACE* ~ EXPR ~ (EOL | &EOI)
        | WHITESPACE* ~ (EOL | INLINE_COMMENT)
    }

//
// Expressions tree
//

EXPR = {
    prefix_op? ~ EOL* ~ TERM ~ postfix_operation* ~ ( EOL* ~ infix_op ~ prefix_op? ~ EOL* ~ TERM ~ postfix_operation*)*
}
    //
    // Postfix
    postfix_operation = _{POSTFIX_DECORATE | POSTFIX_INDEX | POSTFIX_CALL | postfix_arith}
        POSTFIX_DECORATE = {"@" ~ EOL* ~ identifier}
        POSTFIX_EMPTYINDEX = {(symbol_opensquare ~ EOL* ~ symbol_closesquare)}
        POSTFIX_INDEX = {((symbol_opensquare ~ EOL* ~ EXPR ~ EOL* ~ symbol_closesquare) | POSTFIX_EMPTYINDEX)+}
        POSTFIX_CALL = {POSTFIX_NORMALMODE | POSTFIX_OBJECTMODE}
            postfixcall_args = _{(EXPR ~ EOL* ~ symbol_comma ~ EOL*)* ~ EXPR ~ EOL* ~ symbol_comma?}
            POSTFIX_NORMALMODE = {"(" ~ EOL* ~ postfixcall_args? ~ EOL* ~ ")"}
            POSTFIX_OBJECTMODE = {"." ~ EOL* ~ identifier ~ EOL* ~ POSTFIX_NORMALMODE}
        postfix_arith = _{POSTFIX_INC | POSTFIX_DEC}
            POSTFIX_INC = {"++"}
            POSTFIX_DEC = {"--"}
    
    //
    // Prefix
    prefix_op = _{PREFIX_DEL | prefix_arith | PREFIX_BOOL_NOT | PREFIX_BIT_NOT}
        PREFIX_DEL = @{del_keyword ~ (WHITESPACE* ~ symbol_at)?}
            del_keyword = _{("delete" | "del" | "unset") ~ !("_"|ASCII_ALPHANUMERIC)}
        PREFIX_BOOL_NOT = {"!" ~ !"="}
        PREFIX_BIT_NOT = {"~" ~ !"="}
        prefix_arith = _{PREFIX_INC | PREFIX_DEC | PREFIX_NEG}
            PREFIX_NEG = {"-" ~ !"="}
            PREFIX_INC = {"++"}
            PREFIX_DEC = {"--"}

    //
    // Infix
    infix_op = _{assignment_infix_op | OP_BIT_SL | OP_BIT_SR | bool_infix_op | bitwise_infix_op  | arithmetic_infix_op | OP_ASSIGN | match_infix_op | OP_RANGE | OP_CAST | OP_TERNARY}
        //
        // Assignment
        assignment_infix_op = _{
            | OP_ASSIGN_ADD | OP_ASSIGN_SUB | OP_ASSIGN_POW | OP_ASSIGN_MUL | OP_ASSIGN_DIV | OP_ASSIGN_MOD
            | OP_BASSIGN_AND | OP_BASSIGN_OR
            | OP_ASSIGN_AND | OP_ASSIGN_XOR | OP_ASSIGN_OR | OP_ASSIGN_SL | OP_ASSIGN_SR
        }
            OP_ASSIGN_ADD = {"+="}
            OP_ASSIGN_SUB = {"-="}
            OP_ASSIGN_POW = {"**="}
            OP_ASSIGN_MUL = {"*="}
            OP_ASSIGN_DIV = {"/="}
            OP_ASSIGN_MOD = {"%="}
            OP_BASSIGN_AND = {"&&="}
            OP_BASSIGN_OR = {"||="}
            OP_ASSIGN_OR = {"|="}
            OP_ASSIGN_AND = {"&="}
            OP_ASSIGN_XOR = {"^="}
            OP_ASSIGN_SL = {"<<="}
            OP_ASSIGN_SR = {">>="}
            OP_ASSIGN = {"="}

        //
        // Boolean
        bool_infix_op = _{OP_BOOL_OR | OP_BOOL_AND | OP_BOOL_SEQ | OP_BOOL_SNE | OP_BOOL_EQ | OP_BOOL_NE | OP_BOOL_LE | OP_BOOL_GE | OP_BOOL_LT | OP_BOOL_GT}
            OP_BOOL_OR = {"||"}
            OP_BOOL_AND = {"&&"}
            OP_BOOL_SEQ = {"==="}
            OP_BOOL_SNE = {"!=="}
            OP_BOOL_EQ = {"=="}
            OP_BOOL_NE = {"!="}
            OP_BOOL_LE = {"<="}
            OP_BOOL_GE = {">="}
            OP_BOOL_LT = {"<"}
            OP_BOOL_GT = {">"}

        //
        // Bitwise
        bitwise_infix_op = _{OP_BIT_OR | OP_BIT_XOR | OP_BIT_AND | OP_BIT_SL | OP_BIT_SR}
            OP_BIT_OR = {"|"}
            OP_BIT_XOR = {"^"}
            OP_BIT_AND = {"&"}
            OP_BIT_SL = {"<<"}
            OP_BIT_SR = {">>"}

        //
        // Arithmetic
        arithmetic_infix_op = _{OP_ADD | OP_SUB | OP_POW | OP_DIV | OP_MOD | OP_MUL}
            OP_ADD = {"+"}
            OP_SUB = {"-"}
            OP_MUL = {"*"}
            OP_DIV = {"/"}
            OP_MOD = {"%"}
            OP_POW = {"**"}

        //
        // Match
        match_infix_op = _{OP_MATCH_CONTAINS | OP_MATCH_MATCHES | OP_MATCH_IS | OP_MATCH_STARTSWITH | OP_MATCH_ENDSWITH}
            OP_MATCH_CONTAINS = @{"contains" ~ !("_"|ASCII_ALPHANUMERIC)}
            OP_MATCH_MATCHES = @{"matches" ~ !("_"|ASCII_ALPHANUMERIC)}
            OP_MATCH_IS = @{"is" ~ !("_"|ASCII_ALPHANUMERIC)}
            OP_MATCH_STARTSWITH = @{("startswith" | "starts_with") ~ !("_"|ASCII_ALPHANUMERIC)}
            OP_MATCH_ENDSWITH = @{("endswith" | "ends_with") ~ !("_"|ASCII_ALPHANUMERIC)}
            
        //
        // Other
        OP_RANGE = {".."}
        OP_CAST = @{"as" ~ !("_"|ASCII_ALPHANUMERIC)}
        OP_TERNARY = {symbol_questionmark ~ EOL* ~ EXPR ~ EOL* ~ symbol_colon}

//
// Term
// The basic building block of the language
//

TERM = _{
    // These can be identified by the first character
    | "(" ~ EXPR ~ ")"
    | ARRAY_TERM
    | OBJECT_TERM

    // These can be identified by the first word
    | KEYWORD_EXPRESSION

    // Value primitives
    | ATOMIC_VALUE

    // Error catcher
    | ERROR
}

// 1 + 2 [2] ? 3 : 4
// Keyword expressions
//

KEYWORD_EXPRESSION = _ {
    | SKIP_KEYWORD | BREAK_KEYWORD | RETURN_EXPRESSION
    | FOR_LOOP_EXPRESSION | SWITCH_EXPRESSION | IF_EXPRESSION
}

    SKIP_KEYWORD = @{("skip" | "continue") ~ !("_"|ASCII_ALPHANUMERIC)}
    BREAK_KEYWORD = {break_keyword ~ BLOCK?}
        break_keyword = @{"break" ~ !("_"|ASCII_ALPHANUMERIC)}

    RETURN_EXPRESSION = { return_keyword ~ EOL* ~ BLOCK | return_keyword ~ "(" ~ EOL* ~ BLOCK ~ EOL* ~ ")" }
        return_keyword = @{"return" ~ !("_"|ASCII_ALPHANUMERIC)}

    FOR_LOOP_EXPRESSION = { for_keyword ~ EOL* ~ (for_assignment ~ EOL*)? ~ EXPR ~ EOL* ~ (do_keyword | &symbol_opencurly) ~ EOL* ~ BLOCK ~ (for_conditional)? }
        for_assignment = {identifier ~ EOL* ~ in_keyword}
        for_conditional = {if_keyword ~ EOL* ~ EXPR}
        for_keyword = @{"for" ~ !("_"|ASCII_ALPHANUMERIC)}
        in_keyword = @{"in" ~ !("_"|ASCII_ALPHANUMERIC)}
        do_keyword = @{"do" ~ !("_"|ASCII_ALPHANUMERIC)}

    SWITCH_EXPRESSION = { match_keyword ~ EOL* ~ EXPR ~ EOL* ~ symbol_opencurly ~ EOL* ~ (switch_case ~ EOL* ~ symbol_comma ~ EOL*)* ~ switch_case ~ EOL* ~ symbol_comma? ~ EOL* ~ symbol_closecurly }
        switch_case = _{ ATOMIC_VALUE ~ EOL* ~ symbol_arrow ~ EOL* ~ BLOCK }
        match_keyword = @{"match" ~ !("_"|ASCII_ALPHANUMERIC)}

    IF_EXPRESSION = { if_block ~ EOL* ~ (else_keyword ~ EOL* ~ if_block ~ EOL*)* ~ (else_keyword ~ EOL* ~ BLOCK)? }
        if_block = _{ if_keyword ~ EXPR ~ EOL* ~ (then_keyword | &symbol_opencurly) ~ EOL* ~ BLOCK }
        if_keyword = @{"if" ~ !("_"|ASCII_ALPHANUMERIC)}
        then_keyword = @{"then" ~ !("_"|ASCII_ALPHANUMERIC)}
        else_keyword = @{"else" ~ !("_"|ASCII_ALPHANUMERIC)}

    ARRAY_TERM = { 
        array_symbol_opensquare ~ EOL* ~ (
            (ARRAY_TERM|EXPR) ~ (
                EOL* ~ symbol_comma ~ EOL* ~ (ARRAY_TERM|EXPR) ~ EOL*
            )* ~ symbol_comma? ~ EOL*
        )? ~ 
        symbol_closesquare 
    }
        array_symbol_opensquare = {"["}
    OBJECT_TERM = {
        symbol_opencurly ~ EOL* ~ (
            (object_keyvalue_pair ~ EOL* ~ symbol_comma ~ EOL*)* ~ 
            object_keyvalue_pair?
        )? ~ EOL* ~ 
        symbol_closecurly
        
        | symbol_opencurly ~ EOL* ~ symbol_closecurly
    }
        object_keyvalue_pair = _{(OBJECT_TERM|EXPR) ~ symbol_colon ~ (OBJECT_TERM|EXPR)}

//
// Value Literals
// Single literals of the various types
//

ATOMIC_VALUE = _{
    | currency_literal | fixed_literal
    | sci_literal | float_literal
    | int_literal
    | bool_literal | string_literal
    | regex_literal
    | identifier
    | const_literal
}

    fixed_literal = @{
        ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ (^"D" | ^"F")
    }

    // Fixed-point currency values
    // Symbols from [polyvalue::CurrencyInner]
    currency_literal = {
        currency_symbol ~ (float_literal | dec_literal)
        | (float_literal | dec_literal) ~ (currency_symbol | currency_suffix)
    }
        currency_symbol = @{
            "$" | "¢" | "£" | "¤" | "¥" | "֏" | "؋" | "߾" | "߿" | "৲" | "৳" | "৻" | "૱" | "௹" | "฿" | "៛" | "₠" | "₡" |
            "₢" | "₣" | "₤" | "₥" | "₦" | "₧" | "₨" | "₩" | "₪" | "₫" | "€" | "₭" | "₮" | "₯" | "₰" | "₱" | "₲" | "₳" |
            "₴" | "₵" | "₶" | "₷" | "₸" | "₹" | "₺" | "₻" | "₼" | "₽" | "₾" | "₿" | "꠸" | "﷼" | "﹩" | "＄" | "￠" |
            "￡" | "￥" | "￦"
        }
        currency_suffix = @{
            ^"USD" | ^"US$" | ^"CAD" | ^"C$" | ^"AUD" | ^"A$" | ^"NZD" | ^"NZ$" | ^"HKD" | ^"HK$" | ^"SGD" | ^"S$" | ^"EUR" |
            "JPY" | ^"CNY" | ^"CN¥" | ^"GBP" | ^"NOK" | ^"kr" | ^"SEK" | ^"kr" | ^"DKK" | ^"kr" | ^"CHF" | ^"Fr." | ^"ISK" |
            "kr" | ^"CZK" | ^"Kč" | ^"HUF" | ^"Ft" | ^"PLN" | ^"zł" | ^"HRK" | ^"kn" | ^"RUB" | ^"TRY" | ^"BRL" | ^"R$" | ^"INR" |
            "IDR" | ^"Rp" | ^"ILS" | ^"ZAR" | ^"SAR" | ^"AED" | ^"KRW" | ^"VND" | ^"PHP" | ^"MXN" | ^"THB" | ^"MYR" | ^"RM" |
            "TWD" | ^"NT$" | ^"NGN" | ^"CLP" | ^"CL$" | ^"ARS" | ^"AR$" | ^"COP" | ^"CO$" | ^"PEN" | ^"S/." | ^"DOP" | ^"RD$"
        }

    // Floating-point literals
    sci_literal = @{(float_literal | dec_literal) ~ ^"e" ~ ("+"|"-")? ~ ASCII_DIGIT+}
    float_literal = @{dec_literal? ~ "." ~ ASCII_DIGIT+}

    const_literal = @{("pi" | "e" | "tau" | "nil") ~ !("_"|ASCII_ALPHANUMERIC)}

    // Integer literals
    int_literal = {(hex_literal | bin_literal | oct_literal | dec_literal) ~ sized_literal_suffix?}
        dec_literal = @{int_sep | ASCII_DIGIT+}
            int_sep = _{ASCII_DIGIT{1,3} ~ ("_" ~ ASCII_DIGIT{3})+}

        hex_literal = @{(^"0x") ~ ('a'..'f' | 'A'..'F' | ASCII_DIGIT | "_")+}
        bin_literal = @{(^"0b") ~ ('0'..'1' | "_")+}
        oct_literal = @{(^"0o" | "0") ~ ('0'..'7' | "_")+}   

        sized_literal_suffix = _{intsize_u8 | intsize_i8 | intsize_u16 | intsize_i16 | intsize_u32 | intsize_i32 | intsize_u64 | intsize_i64}
            intsize_u8 = {"u8"} intsize_i8 = {"i8"}
            intsize_u16 = {"u16"} intsize_i16 = {"i16"}
            intsize_u32 = {"u32"} intsize_i32 = {"i32"}
            intsize_u64 = {"u64"} intsize_i64 = {"i64"}

    // Other literals
    bool_literal = @{(^"true" | ^"false") ~ !("_"|ASCII_ALPHANUMERIC)}
    string_literal = @{("\"" ~ ("\\"~ANY | (!"\"" ~ ANY))* ~ "\"") | ("\'" ~ ("\\"~ANY | (!"\'" ~ ANY))* ~ "\'")}

    regex_literal = @{
        "/" ~ (!"/" ~ ANY | "\\/")* ~ "/" ~ (ASCII_ALPHA)*
    }

    identifier = @{!reserved_words ~ (ASCII_ALPHA|"_") ~ (ASCII_ALPHANUMERIC|"_")*}
        reserved_words = {
            (
                "if" | "then" | "else" | "for" | "in" | "do" | "match"
                | "return" | "skip" | "continue" | "break"
                | "delete" | "del" | "unset"
                | "as" | "contains" | "matches" | "is" | "startswith" | "starts_with" | "endswith" | "ends_with"
                | "pi" | "e" | "tau" | "nil" | "true" | "false"
            ) ~ !(ASCII_ALPHANUMERIC|"_")
        }

//
// Errors
//

ERROR = _{
    | UNTERMINATED_BLOCK_COMMENT
    | UNTERMINATED_STRING_LITERAL

    | UNCLOSED_BRACKET
    | UNCLOSED_BRACE
    | UNCLOSED_PAREN
    | MISSING_LINEBREAK
}

UNTERMINATED_BLOCK_COMMENT = @{"/*" ~ (!"*/" ~ ANY)*}
UNTERMINATED_STRING_LITERAL = @{"\"" ~ (!EOL ~ ANY)*}
UNCLOSED_BRACKET = @{"[" ~ (!symbol_closesquare ~ ANY)*}
UNCLOSED_BRACE = @{symbol_opencurly ~ (!symbol_closecurly ~ ANY)*}
UNCLOSED_PAREN = @{"(" ~ (!")" ~ ANY)*}
MISSING_LINEBREAK = @{"\\" ~ !EOL}

//
// Symbols
//

symbol_questionmark = {"?"}
symbol_colon = {":"}
symbol_comma = {","}
symbol_arrow = {"=>"}
symbol_at = {"@"}
symbol_eq = {"="}

symbol_opencurly = {"{"}
symbol_closecurly = {"}"}
symbol_opensquare = {"["}
symbol_closesquare = {"]"}